
buildscript {
    ext.agp_version = libs.versions.agp.get()
    ext.dokka_version = libs.versions.dokka.get()
    ext.spaceship_version = libs.versions.spaceship.get()
}

plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
    alias(libs.plugins.dokka)
    alias(libs.plugins.kotlin.jvm)
    alias(libs.plugins.ksp)
    alias(libs.plugins.versions)
    alias(libs.plugins.spaceship.java.lib)
}

group = 'it.scoppelletti.spaceship'
version = '1.1.0'
description 'Spaceship Gradle Plugins for Android projects'
archivesBaseName = 'spaceship-gradle-android'

spaceship {
    url = 'http://github.com/dscoppelletti/spaceship-gradle-android'
    scmUrl = 'git@github.com:dscoppelletti/spaceship-gradle-android.git'
    inceptionYear = '2019-2023'
}

gradlePlugin {
    plugins {
        spaceshipAndroidApp {
            id = 'it.scoppelletti.spaceship.android-app'
            implementationClass = 'it.scoppelletti.spaceship.gradle.android.AppPlugin'
        }
        spaceshipAndroidLibrary {
            id = 'it.scoppelletti.spaceship.android-library'
            implementationClass = 'it.scoppelletti.spaceship.gradle.android.LibraryPlugin'
        }
    }
}

kotlin {
    explicitApi()
    jvmToolchain(17)
}

configurations {
    bom
    api.extendsFrom(bom)
    implementation.extendsFrom(bom)
    testImplementation.extendsFrom(bom)
}

dependencies {
    bom platform(libs.bom.kotlin)
    bom platform(libs.bom.spaceship)
    api gradleApi()
    api "com.android.tools.build:gradle:${agp_version}"
    api "it.scoppelletti.spaceship:spaceship-gradle:${spaceship_version}"
    implementation 'org.apache.commons:commons-lang3'
    implementation 'org.freemarker:freemarker'
    implementation "org.jetbrains.dokka:dokka-gradle-plugin:${dokka_version}"
    testImplementation gradleTestKit()
    testImplementation 'io.kotest:kotest-assertions-core'
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
}

test {
    useJUnitPlatform()
}

