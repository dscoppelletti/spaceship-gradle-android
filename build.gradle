
buildscript {
    ext.agp_version = '7.4.2'
    ext.dokka_version = '1.7.20'
    ext.kotlin_version = '1.8.0'
    ext.KOTLIN_BOM = "org.jetbrains.kotlin:kotlin-bom:${kotlin_version}"
    ext.SPACESHIP_BOM = 'it.scoppelletti.spaceship:spaceship-bom:1.1.0'

    repositories {
        gradlePluginPortal()
        mavenCentral()

        if (project.hasProperty('it.scoppelletti.spaceship.repository.url')) {
            maven {
                url project.property('it.scoppelletti.spaceship.repository.url')
            }
        }
    }
    dependencies {
        classpath platform(KOTLIN_BOM)
        classpath platform(SPACESHIP_BOM)
        classpath 'it.scoppelletti.spaceship:spaceship-gradle'
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.8.0'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'com.github.ben-manes.versions' version '0.46.0'
    id 'org.jetbrains.dokka' version '1.7.20'
}

apply plugin: 'it.scoppelletti.spaceship.java-library'

group = 'it.scoppelletti.spaceship'
version = '1.1.0'
description 'Spaceship Gradle Plugins for Android projects'
archivesBaseName = 'spaceship-gradle-android'

spaceship {
    url = 'http://github.com/dscoppelletti/spaceship-gradle-android'
    scmUrl = 'git@github.com:dscoppelletti/spaceship-gradle-android.git'
    inceptionYear = '2019-2023'
}

gradlePlugin {
    plugins {
        spaceshipAndroidApp {
            id = 'it.scoppelletti.spaceship.android-app'
            implementationClass = 'it.scoppelletti.spaceship.gradle.android.AppPlugin'
        }
        spaceshipAndroidLibrary {
            id = 'it.scoppelletti.spaceship.android-library'
            implementationClass = 'it.scoppelletti.spaceship.gradle.android.LibraryPlugin'
        }
    }
}

kotlin {
    explicitApi()
    jvmToolchain(11)
}

repositories {
    mavenCentral()
    google()

    if (project.hasProperty('it.scoppelletti.spaceship.repository.url')) {
        maven {
            url project.property('it.scoppelletti.spaceship.repository.url')
        }
    }
}

configurations {
    bom
    api.extendsFrom(bom)
    implementation.extendsFrom(bom)
    testImplementation.extendsFrom(bom)
}

dependencies {
    bom platform(KOTLIN_BOM)
    bom platform(SPACESHIP_BOM)
    api gradleApi()
    api "com.android.tools.build:gradle:${agp_version}"
    api 'it.scoppelletti.spaceship:spaceship-gradle'
    implementation 'org.apache.commons:commons-lang3'
    implementation 'org.freemarker:freemarker'
    implementation "org.jetbrains.dokka:dokka-gradle-plugin:${dokka_version}"
    testImplementation gradleTestKit()
    testImplementation 'io.kotest:kotest-assertions-core'
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
}

test {
    useJUnitPlatform()
}

//publishing {
//    publications {
// publishLibraryPublicationToDevRepository
//    ...
//    }
//}
