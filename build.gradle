
buildscript {
    ext.BOM = 'it.scoppelletti.spaceship:spaceship-bom:1.0.0'
    ext.android_gradle_version = '4.1.1'

    repositories {
        jcenter()

        if (project.hasProperty('it.scoppelletti.tools.devRepoUrl')) {
            maven {
                url project.property('it.scoppelletti.tools.devRepoUrl')
            }
        }
    }
    dependencies {
        classpath platform(BOM)
        classpath 'com.github.ben-manes:gradle-versions-plugin'
        classpath 'it.scoppelletti.spaceship:spaceship-gradle-java'
    }
}

plugins {
    id 'java-library'
    id 'java-gradle-plugin'
    id 'maven-publish'
}

apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'it.scoppelletti.spaceship.java-library'

group 'it.scoppelletti.spaceship'
version '1.0.0'
description 'Spaceship Gradle Plugins for Android projects'
archivesBaseName = 'spaceship-gradle-android'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

gradlePlugin {
    plugins {
        spaceshipAndroidApp {
            id = 'it.scoppelletti.spaceship.android-app'
            implementationClass = 'it.scoppelletti.spaceship.gradle.android.AppPlugin'
        }
        spaceshipAndroidLibrary {
            id = 'it.scoppelletti.spaceship.android-library'
            implementationClass = 'it.scoppelletti.spaceship.gradle.android.LibraryPlugin'
        }
        // Deprecated
        spaceshipAndroid {
            id = 'it.scoppelletti.spaceship.spaceship-gradle-android'
            implementationClass = 'it.scoppelletti.spaceship.gradle.android.LibraryPlugin'
        }
    }
}

spaceship {
    url = 'http://github.com/dscoppelletti/spaceship-gradle-android'
    scmUrl = 'git@github.com:dscoppelletti/spaceship-gradle-android.git'
    inceptionYear = '2019-2020'
}

repositories {
    jcenter()
    mavenCentral()
    google()

    if (project.hasProperty('it.scoppelletti.tools.devRepoUrl')) {
        maven {
            url project.property('it.scoppelletti.tools.devRepoUrl')
        }
    }
}

configurations {
    bom
    api.extendsFrom(bom)
    annotationProcessor.extendsFrom(bom)
}

dependencies {
    bom platform(BOM)
    api gradleApi()
    api "com.android.tools.build:gradle:${android_gradle_version}"
    api 'it.scoppelletti.spaceship:spaceship-gradle'
    api 'org.jetbrains.dokka:dokka-gradle-plugin'
    implementation 'org.apache.commons:commons-lang3'
    implementation 'org.thymeleaf:thymeleaf'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'junit:junit'
}

//publishing {
//    publications {
// publishSpaceshipGradlePluginForAndroidProjectsPublicationToDevRepository
//    ...
//    }
//}